{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Solutions to Practice Problems based on Week 7 content"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## General Course Logistics\n",
    "\n",
    "* Work on your project! This week you should finish your implementation and start working on your solution analysis. Think about whether your solution \"makes sense.\" Do you need to make any changes to your model? Add new assumptions? Remove assumptions? Change your input data? Continue working on the report. Remember, writing as you go will save you a lot of time at the end of the semester!\n",
    "\n",
    "\n",
    "## Integer Programming Basics\n",
    "\n",
    "\n",
    "### Logical Modeling practice\n",
    "\n",
    "Let's suppose the CS524 class has decided collectivley to adopt some puppies. There are 6 different breeds ofdogs we can adopt. Each breed has a minimum and maximum number of puppies if we adopt any of that breed (we have to adopt entire litters so no puppies are left alone!). These restrictions, along with the expected happiness we get from each breed, are in the following table:\n",
    "\n",
    "|Breed|Min adopted|Max adopted|Happiness|\n",
    "|:----|:---------|:--------|:------|\n",
    "|Golden retriever|5|7|8|\n",
    "|Shiba Inu|2|10|10|\n",
    "|Great Dane|3|5|4|\n",
    "|Pomeranian|6|13|5|\n",
    "|Water Spaniel (WI state dog!)|3|15|9|\n",
    "|Husky|7|10|6|\n",
    "\n",
    "We have decided that because of the personality of the Shiba Inu, the total number of Shiba Inu puppies adopted should be no more than the combined number of Golden Retriver, Water Spaniel, and Husky puppies. In addition, if any Pomeranians are adopted, we require that at least the minimum number of Great Dane puppies are adopted. We will adopt 40 total dogs and obviously we want to maximize our total happiness. Which breeds of puppies should we adopt, and how many of each?\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only - expires 2022-06-27\r\n",
      "Adopt the following puppies: \n",
      "golden: 7.0\n",
      "shiba: 10.0\n",
      "spaniel: 15.0\n",
      "husky: 8.0\n"
     ]
    }
   ],
   "source": [
    "dogs = [:golden, :shiba, :greatdane, :pomeranian, :spaniel, :husky]\n",
    "min_adopt = Dict(zip(dogs,[5 2 3 6 3 7]))\n",
    "max_adopt = Dict(zip(dogs,[7 10 5 13 15 10]))\n",
    "happ = Dict(zip(dogs,[8 10 4 5 9 6]))\n",
    "\n",
    "using JuMP, Gurobi\n",
    "\n",
    "m = Model(Gurobi.Optimizer)\n",
    "set_optimizer_attribute(m,\"OutputFlag\",0)\n",
    "\n",
    "@variable(m, z[dogs], Bin) # binary variable to determine whether we adopt any dog i\n",
    "@variable(m, x[dogs], Int) # integer variable to determine how many of dog i\n",
    "\n",
    "@objective(m, Max, sum(happ[i]*x[i] for i in dogs)) # maximize happiness\n",
    "\n",
    "# adopt at least the minimimum if we adopt any. enforce the logic x > 0 -> x >= min\n",
    "@constraint(m, min_constr[i in dogs], x[i] >= min_adopt[i]*z[i])\n",
    "# adopt no more than the maximum if we adopt any. enforce the logic z = 0 -> x = 0\n",
    "@constraint(m, max_constr[i in dogs], x[i] <= max_adopt[i]*z[i])\n",
    "\n",
    "# constraint on the total number of Shiba puppies\n",
    "# we can't adopt any shiba puppies\n",
    "# unless we adopt at least that many of the other dogs\n",
    "@constraint(m, x[:shiba] <= x[:golden] + x[:spaniel] + x[:husky])\n",
    "# an alternative interpretation is that the number of shiba puppies can be anything if \n",
    "# no dogs of the other breeds are adopted (only relevant if shiba AND other puppies adopted).\n",
    "# under this interpretation, we want to enforce the logic that if \n",
    "# x[:golden] + x[:spaniel] + x[:husky] > 0, then \n",
    "# x[:golden] + x[:spaniel] + x[:husky] >= x[:shiba]\n",
    "# put a variable in between. x[:golden] + x[:spaniel] + x[:husky] > 0 => delta = 1\n",
    "# or, the contrapositive: delta = 1 => x[:golden] + x[:spaniel] + x[:husky] = 0\n",
    "# then delta = 1 => x[:golden] + x[:spaniel] + x[:husky] >= x[:shiba]\n",
    "# @variable(m, d, Bin)\n",
    "# # constraint that enforces no golden, spaniel, or huskies adopted if delta = 0\n",
    "# @constraint(m, x[:golden] + x[:spaniel] + x[:husky] <= d*40)\n",
    "# # constraint that enforces if delta = 1, then x[:shiba] <= x[:golden] + x[:spaniel] + x[:husky]\n",
    "# # can be found using Slide of Trix\n",
    "# @constraint(m, x[:shiba] - (x[:golden] + x[:spaniel] + x[:husky]) + d*10 <= 10)\n",
    "\n",
    "# cosntraint that z[:pom] = 1 -> x[:greatdane] >= min\n",
    "@constraint(m, x[:greatdane] >= min_adopt[:greatdane]*z[:pomeranian])\n",
    "# adopt 40 dogs\n",
    "@constraint(m, sum(x) == 40)\n",
    "\n",
    "optimize!(m)\n",
    "\n",
    "println(\"Adopt the following puppies: \")\n",
    "for i in dogs\n",
    "    if value(z[i]) > 0.001\n",
    "        println(i, \": \", value(x[i]))\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Set Covering\n",
    "\n",
    "Thanos is here! Once again, Earth is relying on the Avengers for protection. For convenience, the United Nations has divided up the world into 8 diﬀerent zones: North America (NA), South America (SA), Eurasia (EU), North Africa (NF), South Africa (SF), East Asia (EA), South/West Asia (WA), and Oceania (OC). The number of hours required to get from each zone to each other zone by Quinjet is given in the following table:\n",
    "\n",
    "|Zone| NA| SA| EU| NF| SF| EA| WA| OC|\n",
    "|:--|:--|:--|:--|:--|:--|:--|:--|:--|\n",
    "| NA|0| 5 |7| 8| 10 |15| 11| 16| \n",
    "|SA |5| 0| 11| 10 |9 |15| 12| 13|\n",
    "|EU| 7| 11 |0 |4| 7| 9| 5| 19 |\n",
    "|NF |8 |10| 4 |0 |3 |8 |3 |18|\n",
    "|SF| 10 |9 |7 |3 |0 |10| 4| 16|\n",
    "|EA |15| 15| 9| 8| 10 |0 |3| 7 |\n",
    "|WA| 11| 12 |5 |3 |4 |3 |0| 15 |\n",
    "|OC| 16| 13| 19| 18| 16| 7| 15| 0|\n",
    "\n",
    "A rough estimate of the population in each district (in millions) as follows: NA: 580, SA: 420, EU: 900, NF: 250, SF: 200, EA: 1700, WA: 400, OC: 40. The UN have declared there are going to be $n$ Avenger locations. The problem is, the UN isn’t sure how many of the Avengers will be available during the defense against Thanos. Their goal is to maximize the number of people that live within 3 hours of an Avenger. We have been asked to assist the UN by determining where to place the Avengers for diﬀerent values of $n$. Solve the problem for $n = 1,2,3,4$.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Academic license - for non-commercial use only - expires 2022-06-27\r\n",
      "Place Avegner in district WA\n",
      "The following districts will be covered: \n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Academic license - for non-commercial use only - expires 2022-06-27\r\n",
      "Place Avegner in district EU\n",
      "The following districts will be covered: \n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Place Avegner in district WA\n",
      "The following districts will be covered: \n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Academic license - for non-commercial use only - expires 2022-06-27\r\n",
      "Place Avegner in district NA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Place Avegner in district EU\n",
      "The following districts will be covered: \n",
      "NA\n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Place Avegner in district WA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Academic license - for non-commercial use only - expires 2022-06-27\r\n",
      "Place Avegner in district NA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "SA\n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Place Avegner in district SA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "SA\n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Place Avegner in district EU\n",
      "The following districts will be covered: \n",
      "NA\n",
      "SA\n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n",
      "Place Avegner in district WA\n",
      "The following districts will be covered: \n",
      "NA\n",
      "SA\n",
      "EU\n",
      "NF\n",
      "EA\n",
      "WA\n"
     ]
    }
   ],
   "source": [
    "n = 1\n",
    "\n",
    "locs = [:NA, :SA, :EU, :NF, :SF, :EA, :WA, :OC]\n",
    "\n",
    "using NamedArrays\n",
    "\n",
    "trav_time = NamedArray([0 5 7 8 10 15 11 16\n",
    "        5 0 11 10 9 15 12 13\n",
    "        7 11 0 4 7 9 5 19\n",
    "        8 10 4 0 3 8 3 18\n",
    "        10 9 7 3 0 10 4 16\n",
    "        15 15 9 8 10 0 3 7\n",
    "        11 12 5 3 4 3 0 15\n",
    "        16 13 19 18 16 7 15 0], (locs, locs), (\"locs\", \"locs\"))\n",
    "\n",
    "pop = Dict(zip(locs,[580 420 900 250 200 1700 400 40]))\n",
    "\n",
    "cover = NamedArray(zeros(length(locs),length(locs)), (locs, locs), (\"locs\", \"locs\"))\n",
    "for i in locs\n",
    "    for j in locs\n",
    "        if trav_time[i,j] <= 3\n",
    "            cover[i,j] = 1\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "using JuMP, Gurobi\n",
    "\n",
    "# for each possible number of available Avengers\n",
    "for n in [1,2,3,4]\n",
    "    m = Model(Gurobi.Optimizer)\n",
    "    set_optimizer_attribute(m,\"OutputFlag\",0)\n",
    "\n",
    "    # binary variable representing whether we put an Avenger in location i \\in locs\n",
    "    @variable(m, x[locs], Bin)\n",
    "    # binary variable representing whether location i in locs is covered\n",
    "    @variable(m, c[locs], Bin)\n",
    "\n",
    "    # maximize the population covered\n",
    "    @objective(m, Max, sum(pop[j]*c[j] for j in locs))\n",
    "\n",
    "    # constraint that the number of locations we place Avengers = n\n",
    "    @constraint(m, sum(x) == n)\n",
    "    \n",
    "    # cover constraint. If loc j is covered by loc i, this will be 1*x[i] >= c[j]\n",
    "    # this also enforces logic that if we want c[j] = 1, at least one x[i] that covers loc j must be 1\n",
    "    @constraint(m, cov_con[j in locs], sum(cover[i,j]*x[i] for i in locs) >= c[j])\n",
    "    \n",
    "    optimize!(m)\n",
    "    \n",
    "    for i in locs\n",
    "        if value(x[i]) == 1\n",
    "            println(\"Place Avegner in district \", i)\n",
    "            println(\"The following districts will be covered: \")\n",
    "            for j in locs\n",
    "                if value(c[j]) == 1\n",
    "                    println(j)\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.3",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
